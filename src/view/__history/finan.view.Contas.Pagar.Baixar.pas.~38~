unit finan.view.Contas.Pagar.Baixar;

interface

uses
  Winapi.Windows, Winapi.Messages, System.SysUtils, System.Variants, System.Classes, Vcl.Graphics,
  Vcl.Controls, Vcl.Forms, Vcl.Dialogs, Vcl.ExtCtrls, Vcl.StdCtrls,
  System.ImageList, Vcl.ImgList;

type
  TfrmContasPagarBaixar = class(TForm)
    pnlPrincipal: TPanel;
    Panel1: TPanel;
    gbDocumento: TGroupBox;
    Panel2: TPanel;
    gbDetalhes: TGroupBox;
    Label11: TLabel;
    Label3: TLabel;
    Label4: TLabel;
    Label5: TLabel;
    Label6: TLabel;
    lblDocumento: TLabel;
    lblValorParcela: TLabel;
    lblParcela: TLabel;
    lblVencimento: TLabel;
    lblValorAbatido: TLabel;
    Label1: TLabel;
    Label2: TLabel;
    edtValor: TEdit;
    edtObservacao: TEdit;
    Panel3: TPanel;
    btnCancelar: TButton;
    btnBaixar: TButton;
    ImageList1: TImageList;
    procedure btnCancelarClick(Sender: TObject);
    procedure btnBaixarClick(Sender: TObject);
    procedure FormCreate(Sender: TObject);
    procedure edtValorExit(Sender: TObject);
  private
    { Private declarations }
    FID : String;
  public
    { Public declarations }
    procedure baixarContaPagar(ID : String);
  end;

var
  frmContasPagarBaixar: TfrmContasPagarBaixar;

implementation

uses
  sistema.model.Contas.Pagar, sistema.model.Entidades.Conta.Pagar,
  finan.Utilitarios, sistema.model.Entidades.Conta.Pagar.Detalhes,
  sistema.model.usuarios;

{$R *.dfm}

{ TfrmContasPagarBaixar }

procedure TfrmContasPagarBaixar.baixarContaPagar(ID: String);
var
  ContaPagar : TModelContaPagar;
begin
  FID := Trim(ID);
  if FID.IsEmpty then
    raise Exception.Create('ID do contas a pagar inválido!');

  ContaPagar := dmContasPagar.GetContaPagar(FID);
  try
    if ContaPagar.Status = 'B' then
      raise Exception.Create('Não é possível baixar um documento baixado!');

    if ContaPagar.Status = 'C' then
      raise Exception.Create('Não é possível baixar um documento cancelado!');

    if (ContaPagar.ValorAbatido + StrToCurr(edtValor.Text)) > ContaPagar.ValorParcela then
    begin
      Application.MessageBox('O valor digitado está acima do valor da parcela', 'Atenção', MB_OK + MB_ICONWARNING);
      abort;
    end;


    lblDocumento.Caption := ContaPagar.Documento;
    lblParcela.Caption := ContaPagar.Parcela.ToString;
    lblVencimento.Caption := FormatDateTime('dd/mm/yyyy', ContaPagar.DataVencimento);
    lblValorAbatido.Caption := TUtilitarios.FormatarValor(ContaPagar.ValorAbatido);
    lblValorParcela.Caption := TUtilitarios.FormatarValor(ContaPagar.ValorParcela);
    edtObservacao.Text := '';
    edtValor.Text := '';

    Self.ShowModal;
  finally
    ContaPagar.Free;
  end;

end;

procedure TfrmContasPagarBaixar.btnBaixarClick(Sender: TObject);
var
  ContaPagarDetalhe : TModelContaPagarDetalhe;
  ValorAbater : Currency;
begin
  if Trim(edtObservacao.Text) = '' then
  begin
    edtObservacao.SetFocus;
    Application.MessageBox('A observação não pode ser vazia!', 'Atenção', MB_OK + MB_ICONWARNING);
    abort;
  end;

  ValorAbater := 0;
  TryStrToCurr(edtValor.Text, ValorAbater);

  if ValorAbater <= 0 then
  begin
    edtValor.SetFocus;
    Application.MessageBox('O valor não pode ser menor ou igual a zero!', 'Atenção', MB_OK + MB_ICONWARNING);
    abort;
  end;

  ContaPagarDetalhe := TModelContaPagarDetalhe.Create;
  try
    ContaPagarDetalhe.IdContaPagar := FID;
    ContaPagarDetalhe.Detalhes := edtObservacao.Text;
    ContaPagarDetalhe.Valor := ValorAbater;
    ContaPagarDetalhe.Data := Now;
    ContaPagarDetalhe.Usuario := dmUsuarios.GetUsuarioLogado.ID;

    try
      dmContasPagar.BaixarContaPagar(ContaPagarDetalhe);
      Application.MessageBox('Documento baixado com sucesso!', 'Baixado', MB_OK + MB_ICONINFORMATION);
      ModalResult := mrOk;
    except on E : Exception do
      Application.MessageBox(PWideChar(E.Message), 'Erro ao Baixar documento!', MB_OK + MB_ICONWARNING);
    end;

  finally
    ContaPagarDetalhe.Free;
  end;

end;

procedure TfrmContasPagarBaixar.btnCancelarClick(Sender: TObject);
begin
  ModalResult := mrCancel;
end;

procedure TfrmContasPagarBaixar.edtValorExit(Sender: TObject);
begin
  edtValor.Text := TUtilitarios.FormatarValor(edtValor.Text);
end;

procedure TfrmContasPagarBaixar.FormCreate(Sender: TObject);
begin
  edtValor.OnKeyPress := TUtilitarios.KeyPressValor;
end;

end.
